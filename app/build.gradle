apply from: '../repositories.gradle'
apply plugin: 'com.android.application'
apply from: '../dist.gradle'

ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 5
    // exactly 1 digit
    versionMinor = 0
    // exactly 2 digits
    versionBuild = 00
}

android {
    compileSdkVersion 'android-P'
    buildToolsVersion "28.0.0"

    defaultConfig {
        applicationId "com.afwsamples.testdpc"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "targetSdk", "packageName"
    productFlavors {
        targetP {
            dimension 'targetSdk'
            targetSdkVersion 'P'
        }
        targetO {
            dimension 'targetSdk'
            targetSdkVersion 27
        }
        normal {
            dimension 'packageName'
        }
        replica {
            dimension 'packageName'
            applicationIdSuffix ".replica"
        }
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    signingConfigs {
        debug {
            storeFile file("$projectDir/debug.keystore")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    // Enable lint checking in all build variants.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'com.android.support:preference-v14:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.android.gms:play-services-safetynet:+'
    implementation(name: 'setup-wizard-lib-platform-release', ext: 'aar')
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.56'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.56'
    implementation 'com.google.guava:guava:23.6-android'
}
