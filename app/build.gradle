apply from: '../repositories.gradle'
apply plugin: 'com.android.application'
apply from: '../dist.gradle'

ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 7
    // exactly 1 digit
    versionMinor = 0
    // exactly 2 digits
    versionBuild = 03
}

android {
    compileSdkVersion 'android-R'
    buildToolsVersion "28.0.0"

    defaultConfig {
        applicationId "com.afwsamples.testdpc"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "packageName"
    productFlavors {
        normal {
            dimension 'packageName'
        }
        replica {
            dimension 'packageName'
            applicationIdSuffix ".replica"
        }
    }

    lintOptions {
        check 'NewApi'
        abortOnError false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        debug {
            storeFile file("$projectDir/debug.keystore")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    // Enable lint checking in all build variants.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }

    task stripTestOnlyNormalDebug {
        doLast {
            stripTestOnlyForBuild("normal", "debug")
        }
    }

    task stripTestOnlyNormalRelease {
        doLast {
            stripTestOnlyForBuild("normal", "release")
        }
    }

    task stripTestOnlyReplicaDebug {
        doLast {
            stripTestOnlyForBuild("replica", "debug")
        }
    }

    task stripTestOnlyReplicaRelease {
        doLast {
            stripTestOnlyForBuild("replica", "release")
        }
    }

    tasks.whenTaskAdded { task ->
        if (task.name == "splitsDiscoveryTaskNormalDebug") {
            task.dependsOn stripTestOnlyNormalDebug
        } else if (task.name == "splitsDiscoveryTaskNormalRelease") {
            task.dependsOn stripTestOnlyNormalRelease
        } else if (task.name == "splitsDiscoveryTaskReplicaDebug") {
            task.dependsOn stripTestOnlyReplicaDebug
        } else if (task.name == "splitsDiscoveryTaskReplicaRelease") {
            task.dependsOn stripTestOnlyReplicaRelease
        }
    }
}

// Modifies the specified build variant's intermediate AndroidManifest.xml file
// to remove the testOnly attribute. Used for preventing Gradle from inserting the
// attribute when TestDPC is compiled with a development Android branch.
private void stripTestOnlyForBuild(flavor, buildType) {
    def manifestPath =
            "${buildDir}/intermediates/manifests/full/${flavor}/${buildType}/AndroidManifest.xml"
    if (file(manifestPath).exists()) {
        if (System.getenv('TESTDPC_ALLOW_TEST_ONLY') == 'true') {
            println "NOT stripping testOnly from manifest because of TESTDPC_ALLOW_TEST_ONLY"
        } else {
            def xml = new XmlParser().parse(manifestPath)
            def attributeNames = xml.application[0].attributes().collect { it.key }
            attributeNames.each {
                if (it.getLocalPart() == 'testOnly') {
                    xml.application[0].attributes().remove(it)
                    println "Removed testOnly attribute successfully!"
                }
            }
            new XmlNodePrinter(new PrintWriter(new FileWriter(manifestPath))).print(xml)
        }
    }
}

dependencies {
    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation 'androidx.enterprise:enterprise-feedback:1.0.0-alpha02'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation(name: 'setup-wizard-lib-platform-release', ext: 'aar')
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.56'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.56'
    implementation 'com.google.guava:guava:23.6-android'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation "com.google.truth:truth:0.44"
    testImplementation 'junit:junit:4.5'
    testImplementation 'androidx.test:core:1.0.0'
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
}
